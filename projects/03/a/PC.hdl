// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Do the add regardless
    Add16(a=old, b[0]=inc, out=postinc);
    // Toss it out if load is true and use in instead
    Mux16(a=postinc, b=in, sel=load, out=loadorinc);
    // Set to zero if reset == 1, otherwise use the load or inc
    Mux16(a=loadorinc, b=false, sel=reset, out=final);
    // Store the new value no matter what, and feedback
    Register(in=final, load=true, out=old, out=out);
}
